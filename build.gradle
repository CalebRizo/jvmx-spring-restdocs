buildscript {
	ext {
		springBootVersion = '1.5.9.RELEASE'
    spockVersion = '1.1-groovy-2.4'
		restdocsVersion = '2.0.0.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

plugins {
  id "org.asciidoctor.convert" version "1.5.6"
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'org.jvmx.restdocs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

ext {
	snippetsDir = file('build/generated-snippets')
}

ext['spring-restdocs.version'] = restdocsVersion

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-hateoas'
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.codehaus.groovy:groovy'
	runtime 'com.h2database:h2'

	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testCompile "org.spockframework:spock-core:$spockVersion"
  testCompile "org.spockframework:spock-spring:$spockVersion"
	testCompile "org.springframework.restdocs:spring-restdocs-mockmvc"
	asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor:$restdocsVersion"
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

jar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean
